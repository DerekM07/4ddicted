# 2023-01-30

## Blackboard internals

Trying to demystify about `IBlackboard` so-called `DelayedListener`, and here's what people taught me:

### credits to psiberx

you register a listener (delayed or not)
the listener is fired when:
- value has changed with `Set`*()
- value is set and unchanged but `force` parameter is true
- `Signal`*() called explicitly
delayed listener is just called later, in the other part of the cycle.

### Mixing listeners, events and callbacks

But internal mechanics are different from `IBlackboard`, as stated by psiberx.

### as a courtesy of Technic235

```swift
@wrapMethod(ArcadeMachine)
protected func StartGlitching(glitchState:EGlitchState, opt intensity:Float) -> Void {
  if Equals(this.m_controllerTypeName, n"ArcadeMachineController") {
    this.EVMSetupArcadeStaticGlitchListener();
  };
}

@addMethod(InteractiveDevice)
protected func EVMSetupArcadeStaticGlitchListener() -> Void {
  let devicePS = this.GetDevicePS();
  if devicePS.evmArcadeStaticEventID == 0u {
    let evt = new EVMArcadeStaticGlitchEvent();
    let delay: GameTime = GameTime.MakeGameTime(0, 0, 0, RandRange(120, 301)); // days, hours, opt minutes, opt seconds
    devicePS.evmArcadeStaticEventID = GameInstance.GetTimeSystem(devicePS.GetGameInstance()).RegisterDelayedListener(this, evt, delay, -1);
  };
}

public class EVMArcadeStaticGlitchEvent extends Event {
  // intentionally empty
}

@addMethod(ArcadeMachine)
protected cb func OnEVMArcadeStaticGlitchEvent(evt:ref<EVMArcadeStaticGlitchEvent>) {
  let delaySystem = GameInstance.GetDelaySystem(this.GetGame());
  let callback = new EVMDelayArcadeStaticGlitchCallback();
  callback.machine = this;
  delaySystem.DelayCallback(callback, RandRangeF(0, 10), true); // randomize start times
}

class EVMDelayArcadeStaticGlitchCallback extends DelayCallback {
  let machine: ref<ArcadeMachine>;
  protected func Call() -> Void {
    this.machine.EVMStartArcadeStaticGlitch();
  }
}

@addMethod(ArcadeMachine)
protected func EVMStartArcadeStaticGlitch() {
  if !this.evmSparkActive {
    GameObjectEffectHelper.ActivateEffectAction(this, gamedataFxActionType.Start, n"hack_fx");
    let delaySystem = GameInstance.GetDelaySystem(this.GetGame());
    let callback = new EVMArcadeStaticGlitchCompletedCallback();
    callback.machine = this;
    delaySystem.DelayCallback(callback, 13, true);
    this.evmSparkActive = true;
  };
}

class EVMArcadeStaticGlitchCompletedCallback extends DelayCallback {
  let machine: ref<ArcadeMachine>;
  protected func Call() -> Void {
    this.machine.evmSparkActive = false;
  }
}
```